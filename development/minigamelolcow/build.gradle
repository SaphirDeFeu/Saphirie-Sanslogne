plugins {
    id 'java'
    id "xyz.jpenilla.run-paper" version "2.3.0"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'io.github.saphirdefeu'
version = '1.21.4'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
    implementation("org.python:jython-standalone:2.7.4")
    implementation("net.dv8tion:JDA:5.1.0") {
        exclude module: 'opus-java'
    }
    implementation("club.minnced:discord-webhooks:0.8.4")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks {
    runServer {
        minecraftVersion "1.21.4"
    }
}

shadowJar {
    archiveFileName.set("minigamelolcow-1.21.4.jar")

    // Exclude specific files if necessary
    // Example: exclude files to avoid conflicts
    // exclude 'META-INF/*.SF'
    // exclude 'META-INF/*.DSA'
    // exclude 'META-INF/*.RSA'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
